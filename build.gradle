buildscript {
  repositories {
    maven { url "https://plugins.gradle.org/m2/" }
    mavenCentral()
    mavenLocal()
  }
  dependencies {
    classpath "se.bjurr.gradle:conventional-release-gradle-plugin:latest-SNAPSHOT"
  }
}

apply plugin: "se.bjurr.gradle.conventional-release"
apply plugin: "groovy-gradle-plugin"


/**
 * Update README with config
 */
def mainGradle = 'src/main/groovy/se.bjurr.gradle.update-versions.gradle'

def mainGradleContent = file(mainGradle).text
def defaultConfigStart = "// ---- start of default config ----"
def defaultConfigEnd = "// ---- end of default config ----"
def defaultConfig = mainGradleContent.substring(mainGradleContent.indexOf(defaultConfigStart)+defaultConfigStart.size())
defaultConfig = defaultConfig.substring(0, defaultConfig.indexOf(defaultConfigEnd))

def readmeContent = file('README.md').text
def readmeContentStart = "<!-- start default config -->"
def readmeContentEnd = "<!-- end default config -->"
def readmeContentFirstPart = readmeContent.split(readmeContentStart)[0]
def readmeContentSecondPart = readmeContent.split(readmeContentEnd)[1]
def newReadmeContent = """${readmeContentFirstPart}${readmeContentStart}
```groovy
${defaultConfig}
```
${readmeContentEnd}${readmeContentSecondPart}"""
file('README.md').write(newReadmeContent)